# Maintainer: Jose Riha <jose1711 gmail com>
# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.2
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"
source=(http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz
        https://st.suckless.org/patches/alpha/st-alpha-0.8.2.diff
        https://st.suckless.org/patches/solarized/st-no_bold_colors-0.8.1.diff
        st-solarized-dark-custom.diff)
sha256sums=('aeb74e10aa11ed364e1bcc635a81a523119093e63befd2f231f8b0705b15bf35'
            '9c5b4b4f23de80de78ca5ec3739dc6ce5e7f72666186cf4a9c6b614ac90fb285'
            '5a6e2b745c7746228e0ee4e84214e3ac7054e6d451bc5843364e878bb2011e3b'
            '7f625c8ece1b2966b0030ba5af4a3883bb796b219e063b6a7f9369f57ee7f153')

prepare() {
  cd "$pkgname-$pkgver"
  local file
  for file in "${source[@]}"; do
    if [[ "$file" == *.diff || "$file" == *.patch ]]; then
      echo "Apply patch $(basename ${file})"
      patch -Np1 <"$srcdir/$(basename ${file})"
    fi
  done

  sed \
    -e '/char \*font/s/= .*/= "Hack:pixelsize=18:antialias=true:autohint=true";/' \
    -e '/char \*worddelimiters/s/= .*/= " '"'"'`\\\"()[]{}<>|";/' \
    -i config.def.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  # remove to avoid conflict with ncurses
  rm "${pkgdir}/usr/share/terminfo/s/st" "${pkgdir}/usr/share/terminfo/s/st-256color" 
}
